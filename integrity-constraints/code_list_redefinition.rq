PREFIX qb:   <http://purl.org/linked-data/cube#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX spin: <http://spinrdf.org/spin#>

CONSTRUCT {
  [] a spin:ConstraintViolation ;
    spin:violationRoot ?codedProperty ;
    spin:violationPath qb:codeList ;
    spin:violationValue ?redefinedCodeList ; 
    rdfs:label ?violationLabel .
}
WHERE {
  VALUES ?codedPropertyClass {
    qb:CodedProperty
    qb:DimensionProperty
  }
  ?codedProperty a ?codedPropertyClass ;
    qb:codeList ?redefinedCodeList .

  GRAPH <http://data.openbudgets.eu/ontology> {
    ?codedProperty qb:codeList ?originalCodeList .
  }
  FILTER (!sameTerm(?redefinedCodeList, ?originalCodeList))
  BIND (STRLANG(CONCAT("Code list of the core property <",
                       STR(?codedProperty),
                       "> cannot be redefined from <",
                       STR(?originalCodeList),
                       "> to <",
                       STR(?redefinedCodeList),
                       "> . Create a subproperty if you need a custom code list."
                       ), "en") AS ?violationLabel)
}
