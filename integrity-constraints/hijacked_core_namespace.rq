PREFIX qb:     <http://purl.org/linked-data/cube#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX spin:   <http://spinrdf.org/spin#>

CONSTRUCT {
  [] a spin:ConstraintViolation ;
    spin:violationValue ?usedTBox ;
    rdfs:label "Term defined in the core OpenBudgets.eu namespace."@en .
}
WHERE {
  {
    SELECT DISTINCT ?usedTBox
    WHERE {
      GRAPH <http://data.openbudgets.eu/resource/dataset/validated-dataset> {
        {
          [] a ?usedTBox .
        } UNION {
          [] ?usedTBox [] .
        }
        FILTER (STRSTARTS(STR(?usedTBox), "http://data.openbudgets.eu/ontology/")
                &&
                !CONTAINS(SUBSTR(STR(?usedTBox), STRLEN("http://data.openbudgets.eu/ontology/") + 1), "/"))
      }
    }
  }
  GRAPH <http://data.openbudgets.eu/resource/dataset/validated-dataset> {
    ?usedTBox ?p ?o .
    FILTER (!isBlank(?o))
  }
  FILTER NOT EXISTS {
    GRAPH <http://data.openbudgets.eu/ontology> {
      ?usedTBox ?p ?o . 
    }
  }
}
